import java.time.LocalDateTime

plugins {
    id "com.peterabeles.gversion" version "1.10"
    id "io.freefair.lombok" version "8.6"
}

test {
    // Disable test reports to prevent conflicts between parent and child gradle task when running game tests
    reports {
        junitXml.required = false
        html.required = false
    }

    // https://stackoverflow.com/a/36130467
    testLogging {
        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

// Configuration for AdvantageKit
tasks.register("replayWatch", JavaExec) {
    mainClass = "org.littletonrobotics.junction.ReplayWatch"
    classpath = sourceSets.main.runtimeClasspath
}

dependencies {
    implementation 'gov.nist.math:jama:1.0.3'

    def akitJson = new groovy.json.JsonSlurper().parseText(new File(projectDir.getAbsolutePath() + "/vendordeps/AdvantageKit.json").text)
    annotationProcessor "org.littletonrobotics.akit:akit-autolog:$akitJson.version"
}

// Create version file
project.compileJava.dependsOn(createVersionFile)
gversion {
    srcDir = "src/main/java/"
    classPackage = "frc.robot"
    className = "BuildInfo"
    dateFormat = "yyyy-MM-dd HH:mm:ss z"
    timeZone = "America/Los_Angeles"
    indent = "    "
}

["REAL", "SIM", "REPLAY"].collect { mode ->
    var modeDisplay = mode.toLowerCase().capitalize()
    tasks.register("generateBuildConstants$modeDisplay") {
        doLast {
            new File("src/main/java/frc/robot", "BuildConstants.java").withWriter { writer ->
                writer << "package frc.robot;\n"
                writer << "\n"
                writer << "/** Constants generated for mode $mode by build.gradle's generateBuildConstants task at ${new Date().toString()} */\n"
                writer << "public final class BuildConstants {\n"
                writer << "    public static final Mode mode = Mode.$mode;\n"
                writer << "\n"
                writer << "    public enum Mode {\n"
                writer << "        /**\n"
                writer << "         * Real robot\n"
                writer << "         */\n"
                writer << "        REAL,\n"
                writer << "        /**\n"
                writer << "         * Simulation\n"
                writer << "         */\n"
                writer << "        SIM,\n"
                writer << "        /**\n"
                writer << "         * Log replay\n"
                writer << "         */\n"
                writer << "        REPLAY\n"
                writer << "    }\n"
                writer << "}\n"

                writer.flush()
            }
        }
    }
    tasks.register("build$modeDisplay") {
        dependsOn tasks.named("generateBuildConstants$modeDisplay")
        finalizedBy tasks.named("build")
    }
    tasks.register("run$modeDisplay") {
        dependsOn tasks.named("generateBuildConstants$modeDisplay")
        if (mode.toLowerCase() != "real") {
            finalizedBy tasks.named("simulateJava")
        } else {
            finalizedBy tasks.named("deploy")
        }
    }
}

tasks.register("testSim") {
    dependsOn tasks.named("generateBuildConstantsSim")
    finalizedBy tasks.named("test")
}

tasks.register("runRealEvent", Exec) {
    commandLine "git", "commit", "-am", "Deploy auto-commit " + LocalDateTime.now().format("yyyy-MM-dd HH:mm:ss")

    ignoreExitValue = true

    finalizedBy tasks.named("runReal")
}

tasks.register("deployAlerts", JavaExec) {
    mainClass = "DeployAlerts"
    classpath = sourceSets.test.runtimeClasspath
}
tasks.getByName("deploy").dependsOn(tasks.getByName("deployAlerts"))

// https://gist.github.com/falOn-Dev/512c62e009025b3b7569b899bbef955a

boolean isNewer(String versionA, String versionB) {
    def parseVersion = { String version ->
        if (version.startsWith('v')) version = version.substring(1)
        def parts = version.split("\\.")
        return [major: parts[0].toInteger(), minor: parts[1].toInteger(), patch: parts[2].toInteger()]
    }

    def vA = parseVersion(versionA)
    def vB = parseVersion(versionB)

    if (vA.major != vB.major) {
        return vA.major > vB.major
    } else if (vA.minor != vB.minor) {
        return vA.minor > vB.minor
    } else {
        return vA.patch > vB.patch
    }
}


tasks.register("updateVendordeps") {
    description = "Update vendor dependencies"

    def jsonDir = file("vendordeps")
    def slurper = new groovy.json.JsonSlurper()

    doLast {
        jsonDir.eachFile { File file ->
            if (file.name.startsWith("WPILibNewCommands")) return // WPILibNewCommands doesn't have a json url, skip it
            if (file.name.endsWith(".json")) {
                def json = slurper.parse(file)
                def version = json.version
                def url = json.jsonUrl

                try {
                    def remoteJsonText = new URL(url).text
                    def remoteJson = slurper.parseText(remoteJsonText)
                    def remoteVersion = remoteJson.version

                    if (isNewer(remoteVersion, version)) {
                        file.text = remoteJsonText
                        println("Updated ${remoteJson.name} from $version to $remoteVersion")
                    } else {
                        println("${remoteJson.name} is up to date")
                    }
                } catch (Exception e) {
                    println("Failed to update ${file.name}: ${e.message}")
                }
            }
        }
    }
}
